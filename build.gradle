plugins {
    id 'java'
    id 'eclipse'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'net.neoforged.gradleutils' version '3.0.0-alpha.4'
    id 'com.github.ben-manes.versions' version '0.46.0'
}

group 'net.neoforged'
version = gradleutils.version
println('Version: ' + version)

repositories {
    mavenLocal()
    maven { url = 'https://maven.neoforged.net/releases/' }
    mavenCentral()
}

test {
    useJUnitPlatform()
}

license {
    header = file('LICENSE-header.txt')
}

dependencies {
    testImplementation('org.junit.jupiter:junit-jupiter:5.8.2')
    testImplementation('org.junit.jupiter:junit-jupiter-engine:5.8.2')
    testImplementation('org.powermock:powermock-core:2.0.9')
    compileOnly('com.google.code.findbugs:jsr305:3.0.2')
}

java {
    withSourcesJar()
    toolchain.languageVersion = JavaLanguageVersion.of(8)
}

gradleutils {
    setupSigning()
    setupCentralPublishing()
}

changelog {
    from '1.0'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'SRG Utils'
                description = 'Library for managing java obfuscation mappings in various formats.'
                url = 'https://github.com/neoforged/SRGUtils'
                scm {
                    url = 'https://github.com/neoforged/SRGUtils'
                    connection = 'scm:git:git://github.com/neoforged/SRGUtils.git'
                    developerConnection = 'scm:git:git@github.com:neoforged/SRGUtils.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/neoforge/SRGUtils/issues'
                }

                licenses {
                    license {
                        name = 'LGPLv2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }

                developers {
                    developer {
                        id = 'LexManos'
                        name = 'LexManos'
                    }
                }
            }
        }
    }
    repositories {
        maven gradleutils.publishingMaven
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
