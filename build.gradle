import net.neoforged.gradleutils.PomUtilsExtension.License

plugins {
    id 'java'
    id 'eclipse'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'net.neoforged.gradleutils' version '3.0.0-alpha.4'
    id 'com.github.ben-manes.versions' version '0.46.0'
}

group 'net.neoforged'
version = gradleutils.version
println('Version: ' + version)

java.withSourcesJar()
java.withJavadocJar()

repositories {
    mavenLocal()
    maven { url = 'https://maven.neoforged.net/releases/' }
    mavenCentral()
}

test {
    useJUnitPlatform()
}

license {
    skipExistingHeaders = true
    header = file('LICENSE-header.txt')
}

dependencies {
    testImplementation('org.junit.jupiter:junit-jupiter:5.8.2')
    testImplementation('org.junit.jupiter:junit-jupiter-engine:5.8.2')
    testImplementation('org.powermock:powermock-core:2.0.9')
    compileOnly('com.google.code.findbugs:jsr305:3.0.2')
}

java {
    withSourcesJar()
    toolchain.languageVersion = JavaLanguageVersion.of(8)
}

gradleutils {
    setupSigning()
    setupCentralPublishing()
}

changelog {
    from '1.0'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            gradleutils.sign(it)

            pom {
                name = project.name
                description = 'Library for managing java obfuscation mappings in various formats.'

                pomUtils.neoForgedDeveloper(it)
                pomUtils.license(it, License.LGPL_v2)
                pomUtils.githubRepo(it, 'SRGUtils')
            }
        }
    }
    repositories {
        maven gradleutils.publishingMaven
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
